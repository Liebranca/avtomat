#!/usr/bin/perl
# ---   *   ---   *   ---
# OLINK
# Wraps over compiling and
# linking C files with gcc
#
# LIBRE SOFTWARE
# Licensed under GNU GPL3
# be a bro and inherit
#
# CONTRIBUTORS
# lyeb,

# ---   *   ---   *   ---
# deps

package main;

  use v5.36.0;
  use strict;
  use warnings;

  use Cwd qw(abs_path);
  use English qw(-no_match_vars);
  use lib $ENV{'ARPATH'}.'/lib/sys/';
  use lib $ENV{'ARPATH'}.'/lib/';

  use Style;
  use Arstd::Path;
  use Arstd::Array;
  use Arstd::WLog;
  use Arstd::PM;
  use Arstd::IO;

  use Cli;
  use Avt::CRun;

# ---   *   ---   *   ---
# info

  our $VERSION = v1.00.5;
  our $AUTHOR  = 'IBN-3DILA';

  $WLog->ex('olink');

# ---   *   ---   *   ---
# define arguments for commandline

my $m=Cli->nit(

  @{$Cli::Fstruct::ATTRS},

  {id=>'libs',short=>'-l',argc=>1},
  {id=>'incl',short=>'-I',argc=>1},
  {id=>'out',short=>'-o',argc=>1},

  {id=>'debug',short=>'-g',argc=>0},
  {id=>'-pg',short=>'-pg',argc=>0},
  {id=>'run',short=>'-r',argc=>1},
  {id=>'runrm',short=>'-xr',argc=>1},

);

my @files=Cli::Fstruct::proto_search($m);

# ---   *   ---   *   ---
# sanitize input

for my $v($m->{incl},$m->{libs}) {
  $v=$NULLSTR if $v eq $NULL

};

$m->{out}=($m->{out} eq $NULL)
  ? dirof($files[0]) .'/'. nxbasef($files[0])
  : $m->{out}
  ;

# ---   *   ---   *   ---
# separate files by extension

my $by_ext={};

map {

  my $ext=lc extof($ARG);
  $by_ext->{$ext} //= [];

  push @{$by_ext->{$ext}},$ARG;

} @files;

# ---   *   ---   *   ---
# ^roll files together accto
# required frontend

my $fend={
  'Avt::CRun'    => [qw(c cpp)],
  'Avt::flatten' => [qw(s asm inc)],

};

my $by_fend={};

map {

  # ^map [files by ext]
  # ^to  [files by frontend]
  my $key=$ARG;
  my $ext=$fend->{$key};

  $by_fend->{$key}=[map {
    @{$by_ext->{$ARG}}
    if exists $by_ext->{$ARG}

  } @$ext];

  array_filter($by_fend->{$key});

} keys %$fend;

# ---   *   ---   *   ---
# ^invoke frontend

my @bld=();

map {

  # include builder class
  my $class = $ARG;
  my $flist = $by_fend->{$class};

  cload($class);

  # ^cstruc builder ice
  my $exe = $class->new(

    name  => $m->{out},

    libs  => $m->{libs},
    incl  => $m->{incl},

    debug => $m->{debug} ne $NULL,
    files => $flist,

  );


  # ^make objects and save
  $exe->compile();
  push @bld,$exe;


# ^skip if no files avail
} grep {
  @{$by_fend->{$ARG}}

} qw(Avt::flatten Avt::CRun);

# ---   *   ---   *   ---
# ^something went wrong!

if(! @bld) {

  map {

    $WLog->step(
      "unrecognized FF *.$ARG",

    );

  } map {extof($ARG)} @files;


  $WLog->err('aborted',from=>'olink');
  exit -1;

};

# ---   *   ---   *   ---
# ^select linking method

my $obj=$bld[-1];

@{$obj->{files}}=map {
  @{$ARG->{files}}

} @bld[0..$#bld];

$obj->link();

# ---   *   ---   *   ---
# ^get exec [args,flags]

my $rargs = undef;
my $rdel  = 0;

if($m->{run} ne $NULL) {
  $rargs=$m->{run};

} elsif($m->{runrm} ne $NULL) {
  $rargs = $m->{runrm};
  $rdel  = 1;

};

# ---   *   ---   *   ---
# ^exec

if(defined $rargs) {

  $WLog->ex($obj->{bld}->{name});

  $obj->run(split $COMMA_RE,$rargs);
  $obj->rmout() if $rdel;

};

$WLog->step('done');

# ---   *   ---   *   ---
1; # ret
