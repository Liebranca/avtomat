#!/usr/bin/perl
# ---   *   ---   *   ---
# SYGEN
# Creates syntax files from
# available definitions
#
# LIBRE SOFTWARE
# Licensed under GNU GPL3
# be a bro and inherit
#
# CONTRIBUTORS
# lyeb,
# ---   *   ---   *   ---

# deps
  use v5.36.0;
  use strict;
  use warnings;

  use English qw(-no_match_vars);

  use lib $ENV{'ARPATH'}.'/lib/sys/';

  use Style;
  use Arstd::String;
  use Arstd::Array;
  use Chk;

  use Shb7;

  use lib $ENV{'ARPATH'}.'/lib/';

  use Lang;

# ---   *   ---   *   ---
# ROM

  my @COLORS=qw(

    black red green yellow
    blue magenta cyan white

    brightblack

    brightred brightgreen

    brightyellow brightblue

    brightmagenta brightcyan

    brightwhite

  );

# ---   *   ---   *   ---
# ordered list of attrs to use for highlighting

  my @KEYCOLOR=(

    common      => [0x07,'non'],

    hier        => [0x04,'identifier.ns'],
    names_u     => [0x0D,'constant'],
    pfun        => [0x01,'identifier.fn'],
    shcmds      => [0x01,'constant.shcmd'],
    pesc        => [0x0E,'preproc.peso'],

    builtins    => [0x01,'identifier.bltn'],
    fctls       => [0x0D,'statement'],
    types       => [0x04,'type'],
    specifiers  => [0x04,'type.spec'],

    intrinsics  => [0x04,'special'],
    directives  => [0x0D,'identifier.dir'],
    resnames    => [0x04,'identifier.res'],

    nums        => [0x03,'constant.number'],
    vstr        => [0x03,'constant.vstr'],
    ops         => [0x0F,'symbol.operator'],
    chars       => [0x0E,'constant.char'],
    strings     => [0x0E,'constant.nstring'],
    preproc     => [0x0E,'preproc'],

    lcom        => [0x02,'comment'],
    dev0        => [0x0B,'todo'],
    dev1        => [0x09,'error'],
    dev2        => [0x66,'special.blank'],

  );

  # ^hash-ify for convenience
  my %KEYCOLOR=@KEYCOLOR;

# ---   *   ---   *   ---
# remarkable exceptions to the rule

  my $EITHS_NOWB={

    'Perl'=>{types=>1,},
    'Raku'=>{types=>1,},

  };

# ---   *   ---   *   ---
# global state

  my $FNANO    = undef;
  my $FMICRO   = undef;

  my $FMICRO_C = undef;

# ---   *   ---   *   ---
# point to avtomat and retrieve langdefs

Shb7::set_root($ENV{'ARPATH'});
my $Tree=Shb7::walk('avtomat/Lang');

# these files do not output a syntax file
my $EXCLUDED=qr{

  (?: Def[.]pm$)

}x;

# ^get list of files that do
my @Files=grep
  {!($ARG=~ $EXCLUDED)}
  $Tree->get_file_list()

;

# ^import from list
for my $lang(@Files) {

  $lang=Shb7::shpath($lang);
  $lang=~ s[^avtomat/Lang/][];
  $lang=~ s/[.]pm$//;

  # load the package
  eval(q{use Lang::}.$lang.';');

  # ^get reference to instance
  $lang=eval(q{Lang->}.$lang);

};

# ---   *   ---   *   ---
# processes langdef attributes to use
# in a posix regex

sub prprops($def,$key) {

  state $highlights={};
  my ($color,$mname,$v);

  if($key eq 'highlight') {
    $v=shift @{$def->{highlight}};
    $color=shift @{$def->{highlight}};

    $mname=(exists $highlights->{$color})
      ? $highlights->{$color}

      : 'special.high_'.
        chr(0x41+int(keys %$highlights))
      ;

    $highlights->{$color}=$mname;

  } else {
    $v=$def->{$key};
    ($color,$mname)=@{$KEYCOLOR{$key}};

  };

  # decode color
  {

    my ($fg,$bg)=(
      $COLORS[$color&0xF],
      $COLORS[$color>>4]

    );


    if(

       exists $highlights->{$color}
    || $mname eq 'special.blank'

    ) {

      say {$FMICRO_C}
        "color-link $mname \"$fg,$bg\"";

    } elsif(!($mname=~ m[non|special\.blank])) {

      say {$FMICRO_C}
        "color-link $mname \"$fg\"";

    };

    $color="$fg,$bg";

  };

  # nano syntax re was written by a degenerate
  # [\[\]] does not match square brackets

  if($key eq 'ops') {
    $v='([^\s_A-Za-z0-9]|\\\\)';

  };

  # ^he's a double degenerate, the standard
  # string matching patterns are terrible
  my $strtype_re=qr{(?:

    strings
  | chars

  | shcmds
  | preproc
  | pesc

  )}x;

  my ($strtype_beg,$strtype_end);
  my $strtype_skip=q["\\\\\\\\."];

  if($key=~ $strtype_re) {

    if($key eq 'strings') {

      $strtype_beg=q["\""];
      $strtype_end=q["\""];

      $v=Lang::delim(q{"});

    } elsif($key eq 'chars') {

      $strtype_beg=q["'"];
      $strtype_end=q["'"];

      $v=Lang::delim("'");

    } elsif($key eq 'shcmds') {

      $strtype_beg=q["`"];
      $strtype_end=q["`"];

      $v=Lang::delim(q{`});

    } elsif($key eq 'preproc') {

      $strtype_beg=q["^[[:space:]]#"];
      $strtype_end=q["$"];

      $v='^\s*#.*$';

    } else {

      $strtype_beg=q["\\\\$:"];
      $strtype_end=q[";>"];

      $v='\$\:.*;>';

    };

  };

# ---   *   ---   *   ---
# extract patterns

  my @ar=($v);

  if(is_arrayref($v)) {
    @ar=@{$v};

  } elsif(is_hashref($v)) {

    delete $v->{re};

    my @keys=keys %{$v};
    @ar=();

# ---   *   ---   *   ---
# this abomination exists to accomodate sigils

    if(exists $EITHS_NOWB
      ->{$def->{name}}
      ->{$key}

    ) {

      my @exclude=();
      my @noproc=();

      for my $elem(@keys) {
        if(!($elem=~ m/^\(+(\[\$|\\\$)/)) {
          push @exclude,$elem;

        } else {
          push @noproc,$elem;

        };

      };

      @ar=@noproc;
      @keys=@exclude;

    };

# ---   *   ---   *   ---
# output key lists in blocks
# for some reason this is faster

    for(my $x=0;$x<@keys;$x+=32) {

      my $top=(@keys>$x+31)
        ? $x+31 : $#keys;

      my @slice=(@keys[$x..$top]);
      for my $re(@slice) {
        if(is_qre($re)) {
          Lang::qre2re(\$re);

        };

      };

      # sort keys by length
      @slice=sort {
        (length $a)<=(length $b);

      } @slice;

      # join them into a single expression
      my $re;
      if($key eq 'nums') {
        $re='('.(join '|',@slice).')';

      } else {
        $re='\b('.(join '|',@slice).')\b';

      };

      push @ar,$re;


    };

  };

# ---   *   ---   *   ---
# output patterns

  for my $re(@ar) {

    $re=~ s/\s+|\n//sxmg;
    $re=join '\n',(split "\n",$re);

    if(is_qre($re)) {
      Lang::qre2re(\$re);

    };

    my $ore=$re;
    $re=dqwrap($re);

    print {$FNANO} "color $color $re\n";

    $re=~ s[\\][\\\\]sxmg;

    if($key=~ $strtype_re) {

# degenerate micro yaml syntax files
# have sigws python syndrome, as
# changes to the identation of this
# block tend to utterly fuck highlighting
#
#      my $rule  = "    - $mname:\n";
#      $rule    .=
#        "        start: $strtype_beg\n".
#        "        end: $strtype_end\n".
#        "        skip: $strtype_skip\n".
#        "        rules: []\n"
#
#      ;
#
# """sane""" standards, eh?
#
# I give it a 1/10, go back to school
#
# ironically, unholy UBERSCAP regexes
# are better than this modern shit
#
# so, for now, avoid altogether

        $ore=~ s[\\][\\\\]sxmg;
        $ore=~ s["][\\"]sxmg;

        $re=dqwrap($ore);

      };

      print {$FMICRO} "    - $mname: $re\n\n"
      if $mname ne 'non';

  };

};

# ---   *   ---   *   ---
# iters through langdefs
# outs a nanorc file for each

sub make_syntax_files(%O) {

  my $def        = $O{def};

  my $path_nano  = $O{fnano};
  my $path_micro = $O{fmicro};

  open $FNANO,'>',$path_nano
  or die strerr($path_nano);

  open $FMICRO,'>',$path_micro
  or die strerr($path_micro);

  # nano syntax file boiler
  my $prop='syntax '.(

    $def->{name}.' "'.
    $def->{ext}.'"'

  )."\n";

  $prop.='header '.dqwrap($def->{hed})."\n";
  $prop.='magic '.dqwrap($def->{mag})."\n";

  # de-escape comments
  $def->{com}=~ s/\\//sg;

  my $com=$def->{com};
  $def->{lcom}=~ s/\\${com}/$com/sg;

  $prop.='comment '.dqwrap($def->{com})."\n";

  # spit it out
  print {$FNANO} "$prop\n";

# ---   *   ---   *   ---

  my $ext = $def->{ext};
  my $hed = $def->{hed};
  $ext    =~ s[\\][\\\\]sxmg;
  $hed    =~ s[\\][\\\\]sxmg;

  $prop   = "filetype: $def->{name}\n\n";
  $prop  .= "detect:\n".
    "    filename: ".dqwrap($ext)."\n";

  $prop  .=($def->{hed} ne 'N/A')
  ? "    header: ".dqwrap($hed)."\n"
  : $NULLSTR
  ;

  $prop  .= "rules:\n";

  print {$FMICRO} "$prop\n";

# ---   *   ---   *   ---

  # get patterns list
  for my $key(array_keys(\@KEYCOLOR)) {
    prprops($def,$key);

  };

  # overwrite rules
  while(@{$def->{highlight}}) {
    prprops($def,'highlight');

  };

  close $FNANO or die strerr($path_nano);
  close $FMICRO or die strerr($path_micro);

};

# ---   *   ---   *   ---
# setup paths

my $Path         = glob("~/.config");
my $Nano_Path    = "$Path/nano";
my $Micro_Path   = "$Path/micro/syntax";

my $Micro_C_Path = $Path.
  "/micro/colorschemes/widower.micro";

`mkdir -p $Nano_Path`
if ! -d $Nano_Path;

`mkdir -p $Micro_Path`
if ! -d $Micro_Path;

`mkdir -p $Path/micro/colorschemes/`
if ! -d "$Path/micro/colorschemes/";

open $FMICRO_C,'>',$Micro_C_Path
or die strerr($Micro_C_Path);

# ---   *   ---   *   ---
# run the generator

for my $lang(@Files) {

  make_syntax_files(

    # Lang::Def instance
    def    => $lang,

    # destination files
    fnano  => "$Nano_Path/$lang->{name}.nanorc",
    fmicro => "$Micro_Path/$lang->{name}.yaml",

  );

};

# ---   *   ---   *   ---
# complete the micro colorscheme

my %Fmc_Colors=(

  'line-number'         => 'yellow',
  'current-line-number' => 'yellow',

  'status-line'         => 'green,brightblack',
  'selection'           => 'yellow,magenta',
  'tabbar'              => 'green,brightblack',

  'message'             =>
    'brightgreen,brightblack',

  'error-message'       =>
    'brightyellow,brightred',

);

for my $mname(keys %Fmc_Colors) {

  my $color=$Fmc_Colors{$mname};

  say {$FMICRO_C}
    'color-link ',
    "$mname ",

    dqwrap($color)

  ;

};

# ---   *   ---   *   ---
1; # ret
